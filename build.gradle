plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id 'maven-publish'
}

group 'com.github.minecraft-saar'
description 'weight-estimator'

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'release'
    }
}
version = scmVersion.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
mainClassName = "de.saar.minecraft.analysis.WeightEstimator"

repositories {
    mavenCentral()
    maven {url 'https://www.ling.uni-potsdam.de/tcl/maven2/external'}
    maven {url 'https://www.ling.uni-potsdam.de/tcl/maven2/releases'}
    maven { url 'https://jitpack.io' }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.2')
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.16.0'

    // database
    implementation group: 'org.jooq', name: 'jooq', version: '3.14.11'
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.6.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    
    implementation 'org.apache.commons:commons-math3:3.5'

    implementation 'de.bwaldvogel:liblinear:2.42'
    implementation 'info.picocli:picocli:4.6.1'

    implementation group: 'com.github.minecraft-saar.infrastructure', name: 'broker', version: 'release-1.2.7', {
        // we only need the database classes from infrastructure.
        // Prevent the shared-resources from being propagated to dependencies
        // so that this transitive dependency version does not overwrite the other version.
        exclude group: 'com.github.minecraft-saar', module: 'shared-resources'
    }
    implementation 'com.github.coli-saar:basics:9b78dde236d924e696671b586d7e6eb09309f253'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
